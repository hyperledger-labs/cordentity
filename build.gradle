buildscript {
    ext.corda_release_group = 'net.corda'
    ext.corda_release_version = '4.1'
    ext.corda_gradle_plugins_version = '4.0.42'
    ext.quasar_version = '0.7.10'
    ext.junit_version = '4.12'
    ext.kotlin_version = '1.2.71'
    ext.indy_version = "1.8.2"
    ext.jackson_version = '2.9.7'

    repositories {
        jcenter()
        maven { url "https://plugins.gradle.org/m2/" }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlin_version}"
        classpath "net.corda.plugins:cordapp:$corda_gradle_plugins_version"
        classpath "net.corda.plugins:quasar-utils:$corda_gradle_plugins_version"
    }
}

subprojects {

    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
        maven { url 'https://jitpack.io' }
        maven { url 'https://ci-artifactory.corda.r3cev.com/artifactory/corda-releases' }
        maven { url 'https://repo.sovrin.org/repository/maven-releases' }
        maven { url "https://repo.gradle.org/gradle/libs-releases-local/" }
    }

    apply plugin: 'kotlin'
    apply plugin: 'idea'

    
    sourceSets {
        main.resources {
            srcDir "config/dev"
        }
        test.resources {
            srcDir "config/test"
            srcDir "$rootDir/genesis"
        }
    }

    //Required by JitPack for source packing
    apply plugin: 'maven'

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    artifacts {
        archives sourcesJar
    }

    dependencies {
        implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:${kotlin_version}"
        implementation "org.jetbrains.kotlin:kotlin-reflect:${kotlin_version}"

        compile 'com.github.alexeykoren:konfig:2.0.4'

        implementation 'io.github.microutils:kotlin-logging:1.6.22'
        //Version should be same with one used by corda node.
        implementation 'io.reactivex:rxjava:1.2.4'

        testImplementation "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
        testImplementation "junit:junit:$junit_version"
    }

    compileKotlin.kotlinOptions.jvmTarget = "1.8"
    compileTestKotlin.kotlinOptions.jvmTarget = "1.8"
    sourceCompatibility = 1.8
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        jvmTarget = "1.8"
        javaParameters = true   // Useful for reflection.
    }
}